<*
   POET : Parameterized Optimizations for Empirical Tuning
   Copyright (c)  2008,  Qing Yi.  All rights reserved.

Redistribution and use in source and binary forms, with or without modification,
are permitted provided that the following conditions are met:

 1. Redistributions of source code must retain the above copyright notice,
    this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright notice,
    this list of conditions and the following disclaimer in the documentation
    and/or other materials provided with the distribution.
 3. Neither the name of UTSA nor the names of its contributors may be used to
    endorse or promote products derived from this software without specific
    prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT,
INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY
OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISEDOF THE POSSIBILITY OF SUCH DAMAGE.
*>

<*****************************************************************
    This file contains Source-language independent code templates, 
        parse/unparse/rebuild functions, and xform routines 
*****************************************************************>

<************** tokens ************************>

<* represents fix point *>
<xform NullXform pars=(input)>
input
</xform>

<code Char pars=(content:(~"'")....) parse=FALSE >
'@content@'
</code>

<code String pars=(content:((~"\"")...)) parse=FALSE >
"@content@"
</code>

<code FileName pars=(content:((~(" "|"\t"|"\n")....))) rebuild=(content=>STRING) >
@content@
</code>

<code ThrowAway pars=(anything:_) rebuild=""> @anything@ </code>

<define TOKEN (("<" "*") ("*" ">")) />
<code Comment pars=(content:(~"*>")...) rebuild="">
<*@content@*>
</code>
<define TOKEN ""/>

<**** tracing target for all transformations ***>
<define TRACE_TARGET ""/>

<************* parsing functions for code templates*****************>

<* Parse from $input$ a sublist of tokens. Stop if encountering a token that 
   matches the $stop$ pattern or that does not match the $continue_token$ *>
<xform ParseList pars=(input) stop="" code="" continue_token="" output=(result, leftOver)/>

<************** unparsing functions for code templates *****************>

<* line-based unparsing that ensures line break and continuation 
   at certain columes  -- useful in punch-card style languages 
   such as Fortran and Cobol *>
<xform UnparseLine pars=(input, pos) cont_col=6 start_col=8 text_len=70 comment_prefix="" comment_char=""/> 

<**********
The target code template for no parsing
************>
<code NoParse pars=(content:_) > 
@content@
</code>

<**********
Scan a single line of unknown content 
***********>
<code UnknownUntilEOL pars=(content:(~"\n")...) >
@content@
</code>

<*********
Treating a list of IDs as a single name
**********>
<code Name pars=(content: ID....) 
           rebuild=((content:ID)? content : Name#content) >
@content@
</code>

<code NameList parse=LIST(Name,",") />
<code NameOrStringList parse=LIST(String|Name,",") />

<code IntList pars=(content:LIST(INT,",")) rebuild=content>
(@content@)
</code>

<***** look for pattern in input  ******>
<xform Lookup pars=(pattern, input) breakup_pattern=0 >
  foreach cur=pattern \in input do  return TRUE; enddo
  if (breakup_pattern && pattern : CODE.Bop#(_,lhs, rhs)) {
    Lookup(lhs, input) || Lookup(rhs, input)
  }
  else { FALSE }
</xform>

<xform replace_nested_list pars=(from, to, input)>
  switch (input) {
  case (first second) : replace_nested_list(from, to, first) :: replace_nested_list(from, to,second)
  case from : to
  default: input
  }
</xform>

<xform Lookup_nested_list pars=(pattern,input)>
  switch (input) {
  case (first second) : Lookup_nested_list(pattern, first) || Lookup_nested_list(pattern,second)
  case ""|NULL: FALSE
  default: input : pattern
  }
</xform>

<xform Count pars=(pattern, input)>
  res = 0;
  foreach (input : pattern : TRUE) { res = res + 1; }
  res
</xform>

<xform Lookup_r pars=(pattern,input)>
  foreach_r (input : pattern : TRUE) { RETURN TRUE; }
  RETURN FALSE;
</xform>

<*******
collect and returns a list of code within $input$ that matches 
    the given $pattern$ 
*******>
<xform CollectPattern pars=(pattern, input) 
          first_only=0   pattern_is_nested=0  traverse_order=-1 >
  collect = NULL;
  if (traverse_order > 0) {
     foreach cur=pattern \in reverse(input) s.t. !pattern_is_nested do
        if (first_only) RETURN cur;
        collect = cur::collect; 
     enddo
  }
  else {
     foreach cur = pattern \in input s.t. !pattern_is_nested do
        if (first_only) RETURN cur;
        collect = cur::collect; 
     enddo
  }
  collect
</xform>

<xform LookupList pars=(pattern, input)>
  if (input : pattern) { TRUE}
  else if (input : (first second)) 
      { LookupList(pattern,first) || LookupList(pattern,second) } 
  else { FALSE }
</xform>

<xform RemoveFromList pars=(pattern,input)>
  if (input : pattern) { NULL }
  else if (input : (first second)) 
      { r1 = RemoveFromList(pattern,first);
        r2 = RemoveFromList(pattern,second); 
        if (r1 == NULL) { r2 }
        else { r1 :: r2 }
      }  
  else { input }
</xform>

<xform MapList pars=(op, input)>
if (input : "" | NULL) RETURN NULL;
first = op(car(input));
first :: MapList(op, cdr(input))
</xform>

<xform SelectList pars=(index, input)>
if (input : ""|NULL) RETURN NULL;
first = car(input);
for (i = 0; i < index; i = i+1) first = cdr(first);
first = car(first);
first :: SelectList(index, cdr(input))
</xform>

<****** 
Build a list, skipping the components that are empty strings 
*******>
<xform BuildList pars=(first,rest)>
  (first : ""|NULL)? rest 
: (rest : "" | NULL)? first 
: first::rest
</xform>


<*******
Merge two lists into a single one where each item is a pair of the original ones 
********>
<xform MergeList pars=(first,second) op="tuple">
switch (first)
{
  case (first1 first2):
      if (first2!="" && !(second : (second1 second2)))
        ERROR("Mismatching in Merge List:" first " .vs. " second);
     (op == "tuple")? 
        ((first1,second1)::MergeList(first2,second2))
      : (AppendList(first1,second1)::MergeList(first2,second2))
  case "": ""
  case NULL:NULL
  default: (first,second)
}
</xform>

<*******
Append the second list at the end of the first list 
********>
<xform AppendList pars=(first,second) erase_replicate=FALSE >
switch (first) {
 case  (elem1 elem2) :  
    r2 =  AppendList(elem2, second); 
    AppendList(elem1,r2)
 case NULL: second 
 default : 
    if (erase_replicate) {
       for (p = second; p != NULL; p = cdr(p)) {
         if (car(p) == first) return second;
       }
    }
    first :: second
}
</xform>

<*******
 reverse the given list.  
*********>
<xform ReverseList pars=(list) >
  if (list == NULL) RETURN list; 
  result = NULL;
  for (p_list = list; p_list != NULL; p_list = TAIL(p_list)) {
     result = HEAD(p_list) :: result;
  }
  result
</xform>

<*******
flatten a hierarchical list into a flat one 
******>
<xform FlattenList pars=(list) res="">
if (list == NULL) {res}
else {
   result = FlattenList(cdr(list));
   list1 = car(list);
   if (list1 == list) { BuildList(list, result) }
   else { FlattenList[res=result](list1) }
}
</xform>

<****** 
*******>
<xform gen_cartesian_product pars=(l1,l2)>
  res=NULL;
  for (p1 = ReverseList(l1); p1 != NULL; p1 = cdr(p1)) {
    for (p2 = ReverseList(l2); p2 != NULL; p2 = cdr(p2)) {
       res = (car(p1)::car(p2)) :: res;
    }
  }
  res
</xform>

<****** 
split the $input$ at each $split$ separator 
*******>
<xform SplitList pars=(split, input)>
 if (input : (first rest)) {
     if (first == split) { (first, rest) }
     else {
        (before,after) = SplitList(split, first);
        if (after == NULL) {
            (before,after) = SplitList(split, rest);
            ( (first :: before), after) 
        }
        else { (before, ((after==NULL)?rest : (after :: rest) )) }
     }
 }
 else { (input,NULL) }
</xform>


<*******
Erase $handle$ from $trace$. Return the erased $handle$ and modified $trace$
if repl != "", use repl to substitute the erased handle
********>
<xform EraseTraceHandle repl="___keep" pars=(handle, trace) nested_handle=TRUE>
  if (handle : VAR) {
    trace = ERASE(handle, trace);
    for (origvalue=ERASE(handle); origvalue : VAR && nested_handle; origvalue = ERASE(origvalue))
       trace=ERASE(origvalue, trace);
  }
  else origvalue = handle;

   if (repl != "___keep") trace = REPLACE(origvalue, repl, trace);
   (origvalue, trace)
</xform>

<******
Replace handle with a newvalue 
*******>
<xform ModifyTraceHandle pars=(handle, newvalue) nested_handle=TRUE>
   (handle1, newvalue) = EraseTraceHandle(handle, newvalue);
   REPLACE(handle1, newvalue,handle) 
</xform>

<******
Move handle to a new location within trace; 
assume newvalue is already in trace, use it to 
*******>
<xform MoveTraceHandle pars=(handle, newvalue) trace=GLOBAL.TRACE_TARGET nested_handle=TRUE >
 if (trace : VAR || trace != "") {
   (handle1,trace) = EraseTraceHandle[nested_handle=nested_handle](handle, trace);
   handle = REPLACE(handle1, newvalue, handle);
   REPLACE(newvalue, handle, trace);
   handle
 }
 else {
   (handle1, newvalue) = EraseTraceHandle[nested_handle=nested_handle](handle, newvalue);
   REPLACE(handle1, newvalue, handle)
 }
</xform>

<****************** PARSE functions **************************>

<* insert "()" and "," to a list of $elems$ to specify TUPLE parse format *>
<xform BuildTupleParse pars=(elems)/>

<*****
skip empty spaces 
*******>
<xform SkipEmpty pars=(input) >
for (p_input=input; 
     p_input != NULL && ((car p_input) : " "|"\n"|"\t"); 
     p_input=cdr p_input)  {""} 
p_input
</xform>

<*******
return the first token in input 
********>
<xform FirstToken pars=(input) >
if (input == NULL) RETURN "";
for (cur=car input; (cur!=car(cur)); cur = car(cur)) {}
cur
</xform>

<*******
set input to start at the next token; skip empty spaces 
********>
<xform NextToken pars=(input) >
for (p_input=cdr input; 
     p_input != NULL && ((cur=car p_input)==" " || cur == "\n" || cur == "\t"); 
     p_input=cdr p_input)  {""}
p_input
</xform>

<* line-based parsing that skip certain columes -- useful in 
   punch-card style languages such as Fortran and Cobol *>
<xform ParseLine pars=(input) 
          comment_col=-1 continue_col=-1 start_col=1 text_len=66 
          continue_endofline="" comment_prefix="" /> 

<****** implementations of functions/routines *********>
<xform BuildTupleParse pars=(elems)>
 result = "\")\"";
 for (p_elems = ReverseList(elems); p_elems != NULL && cdr(p_elems) != NULL; 
      p_elems = cdr p_elems) {
    result = "\",\"" :: car(p_elems) ::  result; 
  }
 result = "\"(\"" :: HEAD(p_elems) :: result;
 result
</xform>


<xform ParseList pars=(input) stop="" code="" continue_token="" output=(result, leftOver) >
first = car(input);
(first != "")? 
 (
  (first : stop)?  ("", input) 
  : ((continue_token == "" || first : continue_token)? 
     ( 
       (code == "")? (resOfRest = ParseList(cdr(input));
                      BuildList(first,resOfRest[0]), resOfRest[1]) 
        :  (input => (first=code, rest=_); 
            resOfRest=ParseList(rest);
            (BuildList(first,resOfRest[0]),resOfRest[1])) )
  : ("",input) ) )
: (input : stop)?  ("",input) 
: (continue_token == "" || first : continue_token)?  (input, "") 
: ("",input)
</xform>

<xform ParseLine pars=(input) 
          comment_col=-1 continue_col=-1 start_col=1 text_len=66 
          continue_endofline="" comment_prefix=""> 
 res = NULL;
 for ( (curline,input) = SplitList("\n",input); curline!=NULL ; 
       (curline,input) = SplitList("\n",input)) {
   curline = FlattenList(curline);
   if (comment_col > 0) {  <<* skip comments
      curRes =  cdr (SPLIT(comment_col-1, curline));
      (comment curRes1) = SPLIT(1,curRes);
      comment = car(comment);
      if (!(FirstToken(curRes1) : STRING)) <<*QY: this is annotation
          { res=curRes1 :: res; CONTINUE; }
      else if ( !(comment :" "|"\t") 
               && !(comment:INT && car(curRes1):INT)) {
        if (comment_prefix != "") {
          curRes = "\n" :: (comment_prefix^curRes1);
          res = curRes :: res;
        }
        CONTINUE
      }
    }
    else comment = " ";
    curRes = curline;
    if (comment != "\t" && continue_col > 0) { 
        (curRes1 curRes2)=  SPLIT(continue_col-1, curline);
        curRes1=SkipEmpty(curRes1);
        label = FirstToken(curRes1);
        if (label : INT)  { 
           curRes=SkipEmpty(NextToken(SkipEmpty(curline)));
           curRes=label :: curRes; 
           continue_token=" "; }
        else  { (continue_token curRes2) = SPLIT(1,curRes2); curRes= " "::curRes2; }
    } 
    else if (continue_endofline != "") {
       for (p = curRes; p!=NULL; p = cdr(p)) { if (car(p) != "\n") cur = car(p); } 
       if (cur == continue_endofline)
         { 
           nres=NULL;
           for (p = curRes; car(p)!=cur; p = cdr(p)) { nres = car(p) :: nres; }
           curRes=ReverseList("\n"::nres);
           continue_token=cur; }  <<* continue via end-of-line
       else continue_token=" ";
    }
    else continue_token=" ";

    if (text_len > 0)   <<* truncate columes out of \a text_len
      curRes = car (SPLIT(text_len, curRes));

    if (continue_token == " ") <<* QY: not continuation of the previous line
          res = "\n" :: res;
    else curRes=" " :: curRes;
    res = curRes :: res;
 }
 res = ReverseList(res);
 res 
</xform>

<xform UnparseLine pars=(input, pos) label_col=-1 cont_col=6 start_col=8 text_len=70 is_omp=0 comment_prefix="" comment_char="">

if(is_omp) {
  <* Purpose: If input is part of OMP prefix, do not indent it.
   * Pattern (use default attributes):
   * - !$OMP  ... -- new line
   * - !$OMP^ ... -- contd.  *>
  if ((pos == 0 && input == "!")
      || (pos == 1 && input == "$")
      || (pos == 2 && (input =="OMP" || input == "omp"))
      || (pos == 5 && input =="^")) {
    RETURN input;
  }
}

if (comment_prefix != "") {
  (inputPrefix  inputRest) = SPLIT(LEN(comment_prefix), input);
  if (pos == 0 && comment_prefix == inputPrefix) {
    RETURN (comment_char inputRest);
  }
}

<* Skip processing comments. *>
if (pos == 0 && input == "*")
  RETURN input;

  if(input == "\n") RETURN(input);

  input_len = LEN(input);

  diff = pos - start_col;
  if (pos > start_col) {
     if (diff + input_len < text_len || text_len == 0) RETURN input;
     for (i=start_col; i >= cont_col; i -=1) { input = " "::input;  }
     for ((input="*"::input;i-=1); i >= 0; i -=1) 
         { input = " "::input;  }
     input = "\n"::input;
  }
  else if ( (input : INT) && label_col > 0) {
       if (pos > label_col) {
          for (i=1; i < label_col; i +=1) 
             { input = " "::input;  }
          input="\n"::input::" "; 
          
       }
       else {
          for (i=pos+1; i < label_col; i +=1) 
             { input = " "::input;  }
       }
       RETURN input;
  }
  else { for (""; pos < start_col; pos +=1) { input = " "::input;  } }
  
  if (input_len < text_len || text_len == 0) { RETURN input; } 

  (input_curLine  input_nextLine) = SPLIT(input_len-diff-1,input);
  for (i = start_col-1; i >= cont_col; i-=1)
      { input_nextLine = " "::input_nextLine;}
  input_nextLine = "*"::input_nextLine;
  for (i = start_col-1; i >= cont_col; i-=1)
     { input = " "::input_nextLine;}
  input_nextLine = "*"::input_nextLine;
  for (i-=1; i >= 0 ;  i-=1)
     { input_nextLine = " "::input_nextLine;}
  (input_curLine :: "\n" :: input_nextLine)
</xform>

<**** Table/Map functions ***>

<xform LookupTable pars=(key, tab) deep_search=1 >
 if (tab : (first rest)) { 
     res = LookupTable(key, first);
     if (res == "" && deep_search) res = LookupTable(key, rest);
     res
  }
  else { tab[key] }
</xform>

<xform InsertKey pars=(key, val, tab) overwrite=0 > 
 if (tab : (first rest)) { first = InsertKey(key, val, first); (first rest) }
 else if (tab[key] != "") { 
         if (!overwrite) ERROR("value already defined for key: " key "\n in table: " tab); 
         else tab[key]=val; tab}
 else { tab[key] = val; tab }
</xform>
